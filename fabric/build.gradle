plugins {
	id "com.github.johnrengelman.shadow" version "7.0.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

def ENV = System.getenv()

architectury {
	platformSetupLoomIde()
	fabric()
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
	dev
}

def generatedResources = file("src/generated/resources")
def accessWidenerFile = project(":common").file("src/main/resources/ftbchunks.accesswidener")

loom {
	accessWidenerPath = accessWidenerFile
}

sourceSets {
	main {
		resources {
			srcDir generatedResources
		}
	}
}

artifacts {
	dev(jar)
}

repositories {
	maven {
		url "https://maven.blamejared.com"
		content {
			includeGroup "com.faux.ingredientextension"
		}
	}
	maven {
		url "https://maven.nucleoid.xyz/"
		content {
			includeGroup "eu.pb4"
		}
	}
}

dependencies {
	modApi "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
	modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

	modImplementation("dev.ftb.mods:ftb-library-fabric:${rootProject.ftb_library_version}") { transitive = false }
	modImplementation("dev.ftb.mods:ftb-teams-fabric:${rootProject.ftb_teams_version}") { transitive = false }
	modImplementation("dev.ftb.mods:ftb-ranks-fabric:${rootProject.ftb_ranks_version}") { transitive = false }
	modCompileOnlyApi("dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}")
	//modImplementation("dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}")
//	modImplementation("dev.latvian.mods:rhino-fabric:${rootProject.rhino_version}")

//	modImplementation("curse.maven:auth-me-356643:3725468")
	modImplementation("curse.maven:cloth-config-348521:4573053")

	// compile-only because for some reason loom isn't deobfuscating these...
	modCompileOnly("curse.maven:balm-fabric-500525:${balm_fabric_version}")  { transitive = false }
	modCompileOnly("curse.maven:waystones-fabric-500087:${waystones_fabric_version}")  { transitive = false }

	modCompileOnly("eu.pb4:common-protection-api:${common_prot_api_version}") { transitive = false }

	common(project(path: ":common", configuration: "dev")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

	compileOnly "net.luckperms:api:5.4"
}

apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/extra-local-mods.gradle"

// The AW file is needed in :fabric project resources when the game is run.
task copyAccessWidener(type: Copy) {
	from accessWidenerFile
	into generatedResources
}

processResources {
	dependsOn copyAccessWidener
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

sourcesJar {
	dependsOn copyAccessWidener
}

shadowJar {
	configurations = [project.configurations.shadowCommon]
	setArchiveClassifier("dev-shadow")
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveBaseName.set "${rootProject.archives_base_name}-${project.name}"
	archiveClassifier.set null
}

jar {
	setArchiveClassifier("dev")
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id_fabric
			releaseType = project.curseforge_type
			addGameVersion "Fabric"
			addGameVersion rootProject.minecraft_version
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-api'
				requiredDependency 'fabric-api'
				requiredDependency 'ftb-library-fabric'
				requiredDependency 'ftb-teams-fabric'
				optionalDependency 'ftb-ranks-fabric'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			version ftbPublishing.mavenVersion
			from components.java
		}
	}

	repositories {
		if (ftbPublishing.ftbToken) {
			maven {
				url ftbPublishing.ftbURL
				credentials {
					username = ftbPublishing.ftbUser
					password = ftbPublishing.ftbToken
				}
			}
		}

		if (ftbPublishing.sapsToken) {
			maven {
				url ftbPublishing.sapsURL
				credentials {
					username = ftbPublishing.sapsUser
					password = ftbPublishing.sapsToken
				}
			}
		}
	}
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")
