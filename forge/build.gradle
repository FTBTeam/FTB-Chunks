plugins {
	id "com.github.johnrengelman.shadow" version "7.0.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

def ENV = System.getenv()

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
	dev
}

loom {
	accessWidenerPath = project(":common").file("src/main/resources/ftbchunks.accesswidener")
	forge {
		convertAccessWideners = true
		extraAccessWideners.add("ftbchunks.accesswidener")
		mixinConfigs = [
				/*"ftbchunks-forge.mixins.json", */
				"ftbchunks-common.mixins.json"
		]
	}
}

artifacts {
	dev(jar)
}

repositories {
	maven {
		name "ModMaven (JEI)"
		url "https://modmaven.dev/"
		content {
			includeGroup "mezz.jei"
		}
	}
	maven {
		url "https://maven.blamejared.com"
		content {
			includeGroup "net.darkhax.gamestages"
		}
	}
}

dependencies {
	forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
	modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

	modImplementation("dev.ftb.mods:ftb-library-forge:${rootProject.ftb_library_version}") { transitive = false }
	modImplementation("dev.ftb.mods:ftb-teams-forge:${rootProject.ftb_teams_version}") { transitive = false }

	modLocalRuntime("me.shedaniel:RoughlyEnoughItems-forge:9.1.574")

	common(project(path: ":common", configuration: "dev")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/extra-local-mods.gradle"

processResources {
	def file = new File("./build/resources/main/META-INF/mods.toml")
	if (file.exists()) {
		file.delete()
	}

	exclude '.cache'
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version,
				"archversion": project.architectury_version,
				"forgeversion": project.forge_version,
				"forgeshortversion": project.forge_version.split("\\.")[0],
				"mcversion": project.minecraft_version,
				"libraryversion": project.ftb_library_version,
				"teamsversion": project.ftb_teams_version
	}
}

shadowJar {
	exclude "fabric.mod.json"

	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveBaseName.set "${rootProject.archives_base_name}-${project.name}"
	archiveClassifier.set null
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	classifier "dev"
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id_forge
			releaseType = project.curseforge_type
			addGameVersion "Forge"
			addGameVersion rootProject.minecraft_version
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-api'
				requiredDependency 'ftb-library-forge'
				requiredDependency 'ftb-teams-forge'
				optionalDependency 'ftb-ranks-forge'
				optionalDependency 'ftb-xmod-compat'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.FTB_MAVEN_TOKEN) {
			maven {
				url "https://maven.ftb.dev/releases"
				credentials {
					username = "ftb"
					password = "${ENV.FTB_MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "ftb"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")
